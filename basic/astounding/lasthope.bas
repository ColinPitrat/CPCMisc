10 REM********LAST HOPE****
15 DIM N(40,24)
20 INK 1,13
30 INK 2,9
40 INK 3,6
50 INK 0,1
60 ENT 1, 100, -9,1
70 BORDER 1
80 PEN 1:PAPER 0
90 GOSUB 660
100 REM*****MAIN LOOP******
110 LOCATE AA,BB:CALL &BD19:PRINT"  "
120 A=A+1:IF A=39 THEN A=1:B=B+1
130 PEN 2
140 LOCATE A,B:PRINT CHR$ (141); CHR$(140);
150 PEN 1
160 FOR DL=1 TO 30:NEXT DL
170 IF N(A+2, B)=1 THEN GOTO 420:REM***crash***
180 IF A=38 AND B=24 THEN GOTO 540:REM***success***
190 AA=A:BB=B
200 SOUND 2,2000,4,5, 0,1
210 SOUND 1,3000,6,5
220 IF INKEY (18) = 0 THEN GOSUB 240
230 M=M+1:GOTO 100
240 REM****BOMBS AWAY****
250 C=A:D=B+1
260 LOCATE AA,BB:CALL &BD19:PRINT "  "
270 A=A+1:IF A=39 THEN A=1:B=B+1
280 PEN 2
290 LOCATE A,B:PRINT CHR$ (141); CHR$ ( 140):
300 PEN 1
310 IF N(A+2, B)=1 THEN GOTO 420:REM***crash***
320 IF A=39 AND B=24 THEN GOTO 540:REM***success***
330 AA=A:BB=B
335 FRAME
340 PEN 3:LOCATE C,D:PRINT CHR$ (252):PEN 1
350 SOUND 1,D*15,3
360 SOUND 2,2000,3,5,0,1
370 IF N(C, D)=1 THEN N(C, D)=0:SC=SC+ 16
380 LOCATE C,D:PRINT " "
390 LOCATE 7,1:PEN 3:PRINT SC:PEN 1
400 D=D+1:IF D=25 THEN BM=BM+1:RETURN
410 GOTO 260
420 REM*****CRASH*****
430 FOR T=1 TO 20
440 PEN 3:SOUND 1,1500, 17, 15,0,0,5
450 CA=A+INT(RND*4)-INT(RND*2):CB=B+INT(RND*3)-INT(RND*3)
460 IF CA>39 OR CA<1 OR CB>25 THEN GOTO 450
470 LOCATE CA,CB:PRINT CHR$ (238);
480 NEXT T
490 LOCATE 1,25
500 FOR T=1 TO 20:PRINT:SOUND 1,20*T,40:NEXT T
510 LOCATE 10,10
520 PRINT "YOU SCORED "; SC
530 GOTO 580
540 REM*****SUCCESS*****
550 SOUND 1, 100, 150:SOUND 3, 90, 150:SOUND 2, 110, 150
560 CLS:LOCATE 12,10:PRINT" YOU DID IT, WELL DONE!"
570 GOTO 580
580 REM*****PLAY AGAIN*****
590 PRINT:PRINT:PRINT
600 PRINT "             ANOTHER GO?"
619 PRINT
620 PRINT TAB(10);"PRESS 'Y' OR 'N' "
630 A$=INKEY$
640 IF A$="Y" OR A$="y" THEN GOTO 80 ELSE IF A$="N" OR A$="n" THEN END
650 GOTO 630
660 REM*****DRAW BUILDINGS*****
670 CLS:MODE 1
680 PEN 3
690 LOCATE 10,6:PRINT "PLEASE WAIT A MOMENT"
700 PEN 1
710 INK 1,1
720 RANDOMIZE TIME
730 SYMBOL 255, 255, 255, 195, 195, 195, 195, 255, 255
740 FOR I=1 TO 40:FOR J=1 TO 24:N(I,J)=0:NEXT J:NEXT I
750 FOR G=3 TO 38
760 X=INT (RND*16)
770 FOR T=24 TO 24-X STEP -1
780 N(G, T) =1
790 LOCATE G,T
800 PRINT CHR$ (255)
810 NEXT T
820 NEXT G
830 LOCATE 10,6:PRINT"                    "
840 INK 1,13
850 REM*****OTHER INITILISATION*****
860 SC=0:C=0:D=0:A=1:B=2
870 AA=A:BB=B:M=0:BM=0
980 PEN 3
990 LOCATE 1,1:PRINT "SCORE: "
900 PEN 1
910 RETURN
